+++
title = "获取任意位置与圆直径形成的等腰三角形的底边与圆的交点的坐标"
postid = 1097
date = 2010-08-18T11:43:30+08:00
isCJKLanguage = true
toc = false
type = "post"
slug = "circle-hit-test"
aliases = [ "/post/1097.html",]
category = [ "technology",]
tag = [ "as3", "math",]
attachments = [ "1098", "1099",]
+++


### 原理图  

[![判断鼠标位置与圆直径形成的等腰三角形的底边与圆的交点的坐标原理图](/uploads/2010/08/circle_dia.png "circle_dia")](/uploads/2010/08/circle_dia.png)

<!--more-->

### 效果

{{< flash publishmethod="dynamic" fversion="10.0.0" useexpressinstall="true" replaceId="simpleSample" movie="/uploads/2010/08/SimpleSample5.swf" width="400" height="400" targetclass="flashmovie" >}}

### 源码

``` actionscript
package
{
    import flash.display.Sprite;
    import flash.events.MouseEvent;
    
    [SWF(width=400,height=400)]
    public class SimpleSample5 extends Sprite
    {
        private var _centerX:Number;
        private var _centerY:Number;
        private var _radius:Number = 80;
        
        public function SimpleSample5()
        {
            _centerX = stage.stageWidth/2;
            _centerY = stage.stageHeight/2;
            draw();
            drawTriangle(0, 0);
            stage.addEventListener(MouseEvent.MOUSE_MOVE, mouseMoveHandler);
        }
        
        private function draw():void
        {
            graphics.clear();
            graphics.lineStyle(1);
            graphics.moveTo(0, stage.stageHeight/2);
            graphics.lineTo(stage.stageWidth, _centerY);
            graphics.moveTo(_centerX, 0);
            graphics.lineTo(_centerX, stage.stageHeight);
            graphics.drawCircle(_centerX, _centerY, _radius);
        }
        
        private function drawTriangle($mouseX:Number, $mouseY:Number):void
        {
            var __dx:Number = _centerX - $mouseX;
            var __dy:Number = _centerY - $mouseY;
            var __distance:Number = Math.sqrt(__dx*__dx + __dy*__dy);
            //计算点击处与圆心夹角的角度并在坐标系中旋转90度
            var __radian:Number = Math.atan2(__dy, __dx) - Math.PI/2;
            var __x2:Number = _radius * Math.cos(__radian);
            var __y2:Number = _radius * Math.sin(__radian);
            graphics.moveTo(_centerX, _centerY);
            graphics.lineTo($mouseX, $mouseY);
            graphics.lineTo(_centerX+__x2, _centerY+__y2);
            graphics.lineTo(_centerX, _centerY);
            graphics.lineTo(_centerX-__x2, _centerY-__y2);
            graphics.lineTo($mouseX, $mouseY);
        }
        
        private function mouseMoveHandler(evt:MouseEvent):void
        {
            draw();
            drawTriangle(mouseX, mouseY);
        }
    }
}
```
