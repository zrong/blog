+++
title = "获取任意位置与圆的切点的坐标"
postid = 1101
date = 2010-08-18T15:40:11+08:00
isCJKLanguage = true
toc = false
type = "post"
slug = "circle-hit-test2"
aliases = [ "/post/1101.html",]
category = [ "technology",]
tag = [ "as3", "math",]
attachments = [ "1102", "1103",]
+++


刚刚发的那个 [获取任意位置与圆直径形成的等腰三角形的底边与圆的交点的坐标](https://blog.zengrong.net/post/1097.html) 其实没有多大用处，而这个用处就大了，可以在游戏中进行碰撞检测，在碰到圆形障碍物的时候求出最短的运行路径。

### 原理图

[![任意位置与圆切点坐标原理图](/uploads/2010/08/circle_tangent.png "circle_tangent")](/uploads/2010/08/circle_tangent.png)  
<!--more-->

### 运行效果

{{< flash publishmethod="dynamic" fversion="10.0.0" useexpressinstall="true" replaceId="simpleSample6" movie="/uploads/2010/08/SimpleSample6.swf" width="400" height="400" targetclass="flashmovie" >}}

### 源码

**SimpleSample6.as**

``` actionscript
/**
 * 计算鼠标位置与圆的切点坐标
 * */
package
{
    import flash.display.Sprite;
    import flash.events.MouseEvent;
    
    [SWF(width=400,height=400)]
    public class SimpleSample6 extends Sprite
    {
        private var _centerX:Number;
        private var _centerY:Number;
        private var _radius:Number = 80;
        
        public function SimpleSample6()
        {
            _centerX = stage.stageWidth/2;
            _centerY = stage.stageHeight/2;
            draw();
            drawTriangle(0, 0);
            stage.addEventListener(MouseEvent.MOUSE_MOVE, mouseMoveHandler);
        }
        
        private function draw():void
        {
            graphics.clear();
            graphics.lineStyle(1);
            graphics.moveTo(0, stage.stageHeight/2);
            graphics.lineTo(stage.stageWidth, _centerY);
            graphics.moveTo(_centerX, 0);
            graphics.lineTo(_centerX, stage.stageHeight);
            graphics.drawCircle(_centerX, _centerY, _radius);
        }
        
        private function drawTriangle($mouseX:Number, $mouseY:Number):void
        {
            var __dx:Number = _centerX - $mouseX;
            var __dy:Number = _centerY - $mouseY;
            //计算点击处与圆心相对于X轴的夹角
            var __r1:Number = Math.atan2(__dy, __dx);
            //计算点击处与圆心、点击处与切点1这两条线段间的夹角
            var __d1:Number = Math.sqrt(__dx*__dx + __dy*__dy);
            var __r2:Number = Math.asin(_radius/__d1);
            //计算从切点1向圆的垂直直径做垂线形成的直角三角形的一个角
            var __r3:Number = __r1 - __r2;
            //计算坐标系中的角度
            var __r4:Number = __r3 - Math.PI/2;
            //计算切点1相对于圆心的x、y坐标
            var __x1:Number = _radius * Math.cos(__r4);
            var __y1:Number = _radius * Math.sin(__r4);
            
            //计算点击处与切线2相对于X轴的夹角
            var __r5:Number = Math.PI/2 - __r1 - __r2;
            //计算坐标系中的角度
            var __r6:Number = -__r5;
            //计算切点2相对于圆心的x、y坐标
            var __x2:Number = _radius * Math.cos(__r6);
            var __y2:Number = _radius * Math.sin(__r6);
            
            graphics.moveTo(_centerX, _centerY);
            graphics.lineTo($mouseX, $mouseY);
            graphics.lineTo(_centerX+__x1, _centerY+__y1);
            graphics.lineTo(_centerX, _centerY);
            graphics.lineTo(_centerX-__x2, _centerY-__y2);
            graphics.lineTo($mouseX, $mouseY);
        }
        
        private function mouseMoveHandler(evt:MouseEvent):void
        {
            draw();
            drawTriangle(mouseX, mouseY);
        }
    }
}
```
